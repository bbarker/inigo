module Inigo.Async.FS

import Inigo.Async.Promise
import Inigo.Async.Util
import Data.Buffer
import Extra.Buffer
import Inigo.Util.Path.Path

%foreign (promisifyPrimReq "fs" "(path)=>__require_fs.promises.readFile(path,'utf8')")
fs_readFile__prim : String -> promise String

%foreign (promisifyPrimReq "fs" "(path)=>__require_fs.promises.readFile(path)")
fs_readFileBuf__prim : String -> promise Buffer

%foreign (promisifyPrimReq "fs" "(path,contents)=>__require_fs.promises.writeFile(path,contents)")
fs_writeFile__prim : String -> String -> promise ()

%foreign (promisifyPrimReq "fs" "(path,contents)=>__require_fs.promises.writeFile(path,contents)")
fs_writeFileBuf__prim : String -> Buffer -> promise ()

%foreign (promisifyPrimReq "fs" "(path,r)=>__require_fs.promises.mkdir(path,{recursive: r == 0n})")
fs_mkdir__prim : String -> Bool -> promise ()

%foreign (promisifyPrimReq "fs" "(path)=>__require_fs.promises.readdir(path).then(__prim_js2idris_array)")
fs_getFiles__prim : String -> promise (List String)

-- TODO: Include more stat?
%foreign (promisifyPrimReq "fs" "(path)=>__require_fs.promises.stat(path).then((s)=>s.isDirectory() ? 0n : 1n)")
fs_stat__prim : String -> promise Bool

export
fs_readFile : String -> Promise String
fs_readFile path =
  promisify (fs_readFile__prim path)

export
fs_writeFile : String -> String -> Promise ()
fs_writeFile path contents =
  promisify (fs_writeFile__prim path contents)

export
fs_readFileBuf : String -> Promise Buffer
fs_readFileBuf path =
  promisify (fs_readFileBuf__prim path)

export
fs_writeFileBuf : String -> Buffer -> Promise ()
fs_writeFileBuf path contents =
  promisify (fs_writeFileBuf__prim path contents)

export
fs_mkdir : Bool -> String -> Promise ()
fs_mkdir recursive path =
  promisify (fs_mkdir__prim path recursive)

export
fs_getFiles : String -> Promise (List String)
fs_getFiles path =
  promisify (fs_getFiles__prim path)

export
fs_stat : String -> Promise Bool
fs_stat path =
  promisify (fs_stat__prim path)

export
fs_getFilesR : String -> Promise (List String)
fs_getFilesR path =
  doGetFilesR path
  where
  doGetFilesR : String -> Promise (List String)
  doGetFilesR path =
    do
      isDir <- fs_stat path
      if isDir
        then do
          entries <- fs_getFiles path
          let fullEntries = map (joinPath path) entries
          allFiles <- all (map doGetFilesR fullEntries)
          pure (concat allFiles)
        else
          pure [path]
